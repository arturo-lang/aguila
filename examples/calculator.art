do.import {aguila.art}

do ::
    ; globals
    prevNumber: 0
    prevSym: ""

    ; functions
    cleanScreen:  function => [put 'screen ""]
    digitPressed: function => [put 'screen (fetch'screen) ++ &]

    setOperation: function [sym].export:[prevNumber,prevSym][
        prevNumber: fetch'screen
        prevSym: sym
        cleanScreen
    ]

    calculate: function => [
        calc: do ~"|prevNumber||prevSym||fetch'screen|"
        put 'screen calc
        prevNumber: calc
    ]

    ; shortcuts
    btn: function [x][ button x with [x] -> digitPressed x ]
    ope: function [x][ button.danger x with [x] -> setOperation x ]
    clc: function [ ][ button.success.half "=" -> calculate ]
    drk: function [x][ button.dark x -> cleanScreen ]

    ; main ui
    app .fixed .title: "Calculator" .width:400 .height:460 .padding:30 [
        textinput.large.right 'screen "0"
        hbox [ drk "C" drk "~" drk "%" ope "+" ]
        hbox [ btn "1" btn "2" btn "3" ope "-" ]
        hbox [ btn "4" btn "5" btn "6" ope "*" ]
        hbox [ btn "7" btn "8" btn "9" ope "/" ]
        hbox [ btn "." btn "0" clc]
    ]